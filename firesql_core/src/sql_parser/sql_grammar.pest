alpha = _{ 'a'..'z' | 'A'..'Z' }
digit = _{ '0'..'9' }

WHITESPACE = _{ " " | "\t" | NEWLINE }

ident = @{ alpha ~ (alpha | digit | "_")+ }

path = { ident ~ ("/" ~ ident)* }

object_projection = { "*" }
id_projection = { ^":id" }
select_projection = { object_projection | id_projection | ident }
select_ident_list = { select_projection ~ ("," ~ select_projection)* }

where_stmt = { ^"WHERE" ~ where_clauses }
where_clauses = _{ where_condition ~ ( ^"AND" ~ where_condition )* }
where_condition = _{
    comparison | is_null | is_not_null
}
comparison = {
    ident ~ op ~ value
}
op = {"=" | "!=" | ">" | "<" }
value = {
    reference | bool | number | string
}

number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
}

string = ${ "\"" ~ inner_string ~ "\""}
inner_string =  @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    // | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

bool = {^"true" | ^"false"}

reference = { ^"ref" ~ "(" ~ path ~ ")" }

is_null = { ident ~ ^"is null" }
is_not_null = {ident ~ ^"is not null" }



select_stmt = {
    ^"SELECT"
    ~ select_ident_list
    ~ ^"FROM"
    ~ path
    ~ where_stmt?
}
